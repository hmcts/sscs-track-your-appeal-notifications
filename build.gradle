plugins {
    id 'application'
    id 'checkstyle'
    id 'pmd'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.springframework.boot' version '2.5.15'
    id 'uk.gov.hmcts.java' version '0.12.57'
    id 'org.owasp.dependencycheck' version '9.0.6'
    id 'com.github.ben-manes.versions' version '0.47.0'
    id "org.flywaydb.flyway" version '9.16.3'
    id "info.solidsoft.pitest" version '1.9.11'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'uk.gov.hmcts.reform'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

mainClassName = 'uk.gov.hmcts.reform.sscs.TrackYourAppealNotificationsApplication'

pmd {
    toolVersion = "6.55.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

task installLocalGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'config/git/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks')}
    fileMode 0775
}

compileJava.dependsOn installLocalGitHook

task runGitPreCommitTasks {
    dependsOn 'test'
    dependsOn 'pmdMain'
    dependsOn 'pmdTest'
    dependsOn 'dependencyCheckAggregate'
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all {
            def isNonStable = { String version ->
                def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
                def regex = /^[0-9,.v-]+(-r)?$/
                return !stableKeyword && !(version ==~ regex)
            }
            if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                selection.reject('Release candidate')
            }
        }
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 7-10 fails the build, anything lower and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild', 'true') == 'true' ? 7.0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'

    nvd.validForHours = 24

    analyzers {
        retirejs {
            enabled = false
        }
        ossIndex {
            enabled = false
        }
    }
}

pitest {
    targetClasses = ['uk.gov.hmcts.reform.sscs.service.*']
    excludedClasses = ['uk.gov.hmcts.reform.sscs.config.*',
                       'uk.gov.hmcts.reform.sscs.domain.*',
                       'uk.gov.hmcts.reform.sscs.exception.*',
                       'uk.gov.hmcts.reform.sscs.factory.*',
                       'uk.gov.hmcts.reform.sscs.health.*',
                       'uk.gov.hmcts.reform.sscs.TrackYourAppealNotificationsApplication']
    excludedMethods = ['equals', 'hashcode']
    jvmArgs = ['-Xms1G','-Xmx3G']
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
    threads = 20
    verbose = true
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 75
}

sonarqube {
    properties {
        property "sonar.projectName", "SSCS - Track Your Appeal Notifications"
        property "sonar.projectKey", "SSCSTYANS"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/sscs/domain/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/TrackYourAppealNotificationsApplication.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/servicebus/messaging/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/config/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/utils/GetAllTemplatesFromNotify.java,"
        property "sonar.host.url", "https://sonar.reform.hmcts.net/"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

bootRun {
    systemProperty 'server.port', '8081'
}

ext {
    springCloudVersion = '2020.0.6'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
    dependencies {
        //CVE-2022-41881, CVE-2021-43797, CVE-2023-34462, CVE-2022-24823 - brought in by qpid-jms-client
        dependencySet(group: 'io.netty', version: '4.1.96.Final') {
            entry 'netty-handler'
            entry 'netty-transport-native-epoll'
            entry 'netty-transport-native-kqueue'
            entry 'netty-codec-http'
            entry 'netty-codec'
            entry 'netty-transport-native-unix-common'
            entry 'netty-transport'
            entry 'netty-buffer'
            entry 'netty-resolver'
            entry 'netty-common'
        }

        // resolves: CVE-2019-10101, CVE-2019-10102, CVE-2019-10103
        dependencySet(group: 'org.jetbrains.kotlin', version: '1.9.22') {
            entry 'kotlin-stdlib'
            entry 'kotlin-stdlib-common'
            entry 'kotlin-stdlib-jdk7'
            entry 'kotlin-stdlib-jdk8'
        }

        //CVE-2022-22978, CVE-2022-22976
        dependencySet(group: 'org.springframework.security', version: '5.5.8') {
            entry 'spring-security-crypto'
        }

        //CVE-2021-22118, CVE-2022-22970, CVE-2022-22971
        dependencySet(group: 'org.springframework', version: '5.3.27') {
            entry 'spring-aop'
            entry 'spring-aspects'
            entry 'spring-beans'
            entry 'spring-context'
            entry 'spring-context-support'
            entry 'spring-core'
            entry 'spring-expression'
            entry 'spring-jcl'
            entry 'spring-jdbc'
            entry 'spring-jms'
            entry 'spring-messaging'
            entry 'spring-orm'
            entry 'spring-test'
            entry 'spring-tx'
            entry 'spring-web'
            entry 'spring-webmvc'
        }

        dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.86') {
            entry 'tomcat-embed-core'
            entry 'tomcat-embed-websocket'
        }

        //CVE-2021-22044
        dependencySet(group: 'org.springframework.cloud', version: '3.0.5') {
            entry 'spring-cloud-starter-openfeign'
            entry 'spring-cloud-openfeign-core'
        }

        //CVE-2022-25857
        dependencySet(group: 'org.yaml', version: '1.33') {
            entry 'snakeyaml'
        }

        //CVE-2021-42550
        dependencySet(group: 'ch.qos.logback', version: '1.2.11') {
            entry 'logback-core'
            entry 'logback-classic'
        }
    }

}

repositories {

    mavenLocal()

    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/IntegrationTests/java')
        }
    }
    e2e {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/e2e/java')
        }
        resources {
            srcDir('src/e2e/resources')
        }
    }

}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

task integration(type: Test) {
    group = 'verification'
    setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
    setClasspath(sourceSets.integrationTest.runtimeClasspath)
}

task smoke(type: Test) {
    group = 'verification'
    description = 'Executes non-destructive smoke tests against a running CCD and notification service'
    setTestClassesDirs(sourceSets.e2e.output.classesDirs)
    setClasspath(sourceSets.e2e.runtimeClasspath)
    include "uk/gov/hmcts/reform/sscs/smoke/**"
    exclude "uk/gov/hmcts/reform/sscs/functional/**"
}

task functional(type: Test) {
    group = 'verification'
    description = 'Executes non-destructive functional tests in AAT against a running CCD and notification service'
    setTestClassesDirs(sourceSets.e2e.output.classesDirs)
    setClasspath(sourceSets.e2e.runtimeClasspath)
    include "uk/gov/hmcts/reform/sscs/functional/**"
    exclude("uk/gov/hmcts/reform/sscs/smoke/**", "**/*CohNotificationFunctionalTest*")
}

task fortifyScan(type: JavaExec)  {
    main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

configurations {
    integrationTestImplementation.extendsFrom(testImplementation)
    integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
    e2eImplementation.extendsFrom(testImplementation)
    e2eRuntimeOnly.extendsFrom(testRuntimeOnly)
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.required = true
        csv.required = false
        xml.destination file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

tasks.withType(Copy).all { duplicatesStrategy 'warn' }

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
check.dependsOn integration
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)
integration.shouldRunAfter(checkstyleIntegrationTest)
integration.shouldRunAfter(test)
functional.shouldRunAfter(checkstyleIntegrationTest)
functional.shouldRunAfter(test)

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group in ['com.fasterxml.jackson.core', 'com.fasterxml.jackson.module', 'com.fasterxml.jackson.datatype']) {
                details.useVersion '2.11.0'
            }
        }
    }
}

dependencies {
    implementation group: 'commons-io', name: 'commons-io', version: '2.15.0'

    implementation group: 'com.mchange', name: 'c3p0', version: '0.9.5.5'
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '9.22.3'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.6.0'
    implementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.14.9'
    implementation group: 'org.elasticsearch', name: 'elasticsearch', version: '7.17.9'

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.10.RELEASE'
    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.4'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.7.0'
    implementation group: 'io.rest-assured', name: 'rest-assured', version: '5.4.0'
    implementation group: 'io.rest-assured', name: 'json-path', version: '5.4.0'
    implementation group: 'io.rest-assured', name: 'xml-path', version: '5.4.0'

    implementation group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.17.0'
    implementation group: 'org.springframework', name: 'spring-jms'
    implementation group: 'org.apache.qpid', name: 'qpid-jms-client', version: '1.11.0'
    implementation group: 'org.jadira.usertype', name: 'usertype.core', version: '7.0.0.CR1'

    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.0.1'

    implementation group: 'com.github.hmcts', name: 'properties-volume-spring-boot-starter', version: '0.1.1'
    implementation group: 'com.github.hmcts', name: 'java-logging', version: '6.0.1'
    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.1.2'
    implementation group: 'com.github.hmcts', name: 'ccd-client', version: '4.9.2'

    implementation group: 'com.github.hmcts', name: 'cmc-pdf-service-client', version: '7.0.1'

    implementation group: 'com.github.hmcts', name: 'sscs-common', version: '5.3.3'
    implementation group: 'com.github.hmcts', name: 'sscs-pdf-email-common', version: '5.2.5'

    implementation group: 'com.github.hmcts', name: 'sscs-job-scheduler', version: '1.5.7', {
        exclude group: "uk.gov.hmcts.reform", module:"properties-volume-spring-boot-starter"
    }

    implementation group: 'org.json', name: 'json', version: '20230618'

    implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '4.1.0-RELEASE'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.12.0'
    implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.5'
    implementation group: 'io.github.openfeign.form', name: 'feign-form', version: '3.8.0'

    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.27'

    implementation group: 'com.microsoft.azure', name: 'applicationinsights-logging-logback', version: '2.6.4'
    implementation group: 'com.microsoft.azure', name: 'applicationinsights-spring-boot-starter', version: '2.6.4'

    implementation group: 'org.pitest', name: 'pitest', version: '1.12.0'
    implementation group:'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: '1.9.11'
    implementation group:'org.codehaus.sonar-plugins', name:'sonar-pitest-plugin', version: '0.5'

    implementation group: 'org.springframework.boot', name: 'spring-boot-devtools'

    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.30'

    implementation group: 'commons-io', name: 'commons-io', version: '2.15.0'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

    implementation group: 'net.minidev', name: 'accessors-smart', version: '2.5.0'
    implementation group: 'net.minidev', name: 'json-smart', version: '2.5.0'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    testImplementation group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    integrationTestImplementation group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'

    testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.6.2') {
        exclude group: "com.vaadin.external.google", module:"android-json"
    }
    testImplementation platform(group: 'org.junit', name: 'junit-bom', version: '5.9.2')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter'
    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.11.0'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.1', classifier: 'all', {
        exclude group: 'commons-io', module: 'commons-io'
    }

    integrationTestImplementation group: 'commons-io', name: 'commons-io', version: '2.15.0'

    runtimeOnly group: 'org.springframework.boot', name: 'spring-boot-properties-migrator'
}

bootJar {
    archiveName 'track-your-appeal-notifications.jar'
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

run {
    def debug = System.getProperty("debug")
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5006']
    }
}

def dbHost = System.env.JOB_SCHEDULER_DB_HOST ?: "localhost"
def dbPort = System.env.JOB_SCHEDULER_DB_PORT ?: 5432
def dbName = System.env.JOB_SCHEDULER_DB_NAME ?: "sscsjobscheduler"
def dbUser = System.env.JOB_SCHEDULER_DB_USERNAME ?: "sscsjobscheduler"
def dbPass = System.env.JOB_SCHEDULER_DB_PASSWORD ?: "sscsjobscheduler"

flyway {
    url = 'jdbc:postgresql://' + dbHost + ':' + dbPort + '/' + dbName
    user = dbUser
    password = dbPass
}
