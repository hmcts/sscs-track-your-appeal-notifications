plugins {
    id 'application'
    id 'checkstyle'
    id 'pmd'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.5.2'
    id 'uk.gov.hmcts.java' version '0.12.12'
    id 'org.owasp.dependencycheck' version '6.2.2'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'net.ltgt.apt' version '0.21'
    id "org.flywaydb.flyway" version "7.11.3"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'uk.gov.hmcts.reform'
version = '1.0.0'

sourceCompatibility = 11
targetCompatibility = 11

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

mainClassName = 'uk.gov.hmcts.reform.sscs.TrackYourAppealNotificationsApplication'

pmd {
    toolVersion = "6.5.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all {
            def isNonStable = { String version ->
                def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
                def regex = /^[0-9,.v-]+(-r)?$/
                return !stableKeyword && !(version ==~ regex)
            }
            if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                selection.reject('Release candidate')
            }
        }
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    suppressionFile = 'dependency-check-suppressions.xml'
}

sonarqube {
    properties {
        property "sonar.projectName", "SSCS - Track Your Appeal Notifications"
        property "sonar.projectKey", "SSCSTYANS"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/sscs/domain/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/TrackYourAppealNotificationsApplication.java," +
                "src/main/java/uk/gov/hmcts/reform/sscs/servicebus/messaging/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/config/**," +
                "src/main/java/uk/gov/hmcts/reform/sscs/utils/GetAllTemplatesFromNotify.java,"
    }
}

bootRun {
    systemProperty 'server.port', '8081'
}

ext {
    springCloudVersion = 'Hoxton.SR12'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
    dependencies {
        // Solves CVE-2019-10086
        dependency group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'

        dependency group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
        dependency group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.14.1'

        // resolves: CVE-2020-9489
        dependency group: 'org.apache.tika', name: 'tika-core', version: '2.0.0'

        // checkstyle needs a certain version and so does spring fox swagger, so can't use latest
        dependencySet(group: 'com.google.guava', version: '30.1.1-jre') {
            entry 'guava'
        }
        // CVE-2019-0232, CVE-2019-0199 - command line injections on windows
        // CVE-2021-33037
        dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.50') {
            entry 'tomcat-embed-core'
            entry 'tomcat-embed-el'
            entry 'tomcat-embed-websocket'
        }

        // CVE-2020-13954
        dependencySet(group: 'org.apache.cxf', version: '3.4.4') {
            entry 'cxf-core'
            entry 'cxf-rt-frontend-jaxrs'
            entry 'cxf-rt-rs-client'
            entry 'cxf-rt-security'
            entry 'cxf-rt-transports-http'
        }

        dependencySet(group: 'org.apache.xmlgraphics', version: '1.14') {
            entry 'batik-all'
        }

        // CVE-2019-16869 - brought in by qpid-jms-client
        dependencySet(group: 'io.netty', version: '4.1.66.Final') {
            entry 'netty-handler'
            entry 'netty-transport-native-epoll'
            entry 'netty-transport-native-kqueue'
            entry 'netty-codec-http'
            entry 'netty-codec'
            entry 'netty-transport-native-unix-common'
            entry 'netty-transport'
            entry 'netty-buffer'
            entry 'netty-resolver'
            entry 'netty-common'
        }

        // solves CVE-2019-13990
        dependencySet(
                group: 'org.quartz-scheduler',
                version: '2.3.2'
        ) {
            entry 'quartz'
        }

        // resolves: CVE-2019-10101, CVE-2019-10102, CVE-2019-10103
        dependencySet(group: 'org.jetbrains.kotlin', version: '1.5.21') {
            entry 'kotlin-stdlib'
            entry 'kotlin-stdlib-common'
        }

        //CVE-2021-22112
        dependencySet(group: 'org.springframework.security', version: '5.5.0') {
            entry 'spring-security-crypto'
        }

        //CVE-2021-27807
        dependencySet(group: 'org.apache.pdfbox', version: '2.0.24') {
            entry 'pdfbox'
            entry 'pdfbox-tools'
            entry 'preflight'
            entry 'xmpbox'
        }

        //CVE-2021-22118
        dependencySet(group: 'org.springframework', version: '5.2.15.RELEASE') {
            entry 'spring-aop'
            entry 'spring-aspects'
            entry 'spring-beans'
            entry 'spring-context'
            entry 'spring-context-support'
            entry 'spring-core'
            entry 'spring-expression'
            entry 'spring-jcl'
            entry 'spring-jdbc'
            entry 'spring-jms'
            entry 'spring-messaging'
            entry 'spring-orm'
            entry 'spring-test'
            entry 'spring-tx'
            entry 'spring-web'
            entry 'spring-webmvc'
        }

        //CVE-2021-27568
        dependencySet(group: 'net.minidev', version: '2.4.7') {
            entry 'json-smart'
            entry 'accessors-smart'
        }
    }

}

repositories {

    mavenLocal()

    mavenCentral()
    maven { url 'https://jitpack.io' }
    jcenter()
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/IntegrationTests/java')
        }
    }
    e2e {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/e2e/java')
        }
        resources {
            srcDir('src/e2e/resources')
        }
    }

}

task integration(type: Test) {
    group = 'verification'
    setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
    setClasspath(sourceSets.integrationTest.runtimeClasspath)
}

task smoke(type: Test) {
    group = 'verification'
    description = 'Executes non-destructive smoke tests against a running CCD and notification service'
    setTestClassesDirs(sourceSets.e2e.output.classesDirs)
    setClasspath(sourceSets.e2e.runtimeClasspath)
    include "uk/gov/hmcts/reform/sscs/smoke/**"
    exclude "uk/gov/hmcts/reform/sscs/functional/**"
}

task functional(type: Test) {
    group = 'verification'
    description = 'Executes non-destructive functional tests in AAT against a running CCD and notification service'
    setTestClassesDirs(sourceSets.e2e.output.classesDirs)
    setClasspath(sourceSets.e2e.runtimeClasspath)
    include "uk/gov/hmcts/reform/sscs/functional/**"
    exclude("uk/gov/hmcts/reform/sscs/smoke/**", "**/*CohNotificationFunctionalTest*")
}

configurations {
    integrationTestCompile.extendsFrom(testCompile)
    integrationTestRuntime.extendsFrom(testRuntime)
    e2eCompile.extendsFrom(testCompile)
    e2eRuntime.extendsFrom(testRuntime)
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.enabled = true
        csv.enabled = false
        xml.destination file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
check.dependsOn integration
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)
integration.shouldRunAfter(checkstyleIntegrationTest)
integration.shouldRunAfter(test)
functional.shouldRunAfter(checkstyleIntegrationTest)
functional.shouldRunAfter(test)

def versions = [
        logback         : '1.2.3',
        reformLogging   : '5.1.7',
        springHystrix   : '1.4.2.RELEASE',
        springfoxSwagger: '2.8.0',
        restAssured     : '4.3.3'
]

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group in ['com.fasterxml.jackson.core', 'com.fasterxml.jackson.module', 'com.fasterxml.jackson.datatype']) {
                details.useVersion '2.11.0'
            }
            if (details.requested.group == 'org.codehaus.groovy') {
                details.useVersion "3.0.7"
                details.because "needed by rest-assured>=4.3"
            }
        }
    }
}

dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.11.0'

    compile group: 'com.mchange', name: 'c3p0', version: '0.9.5.5'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '6.5.6'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.23'
    compile group: 'net.bytebuddy', name: 'byte-buddy', version: '1.11.8'
    compile group: 'org.elasticsearch', name: 'elasticsearch', version: '7.13.4'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix', version: versions.springHystrix
    compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.1'

    compile group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    compile group: 'io.rest-assured', name: 'json-path', version: versions.restAssured
    compile group: 'io.rest-assured', name: 'xml-path', version: versions.restAssured
    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger
    compile group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.17.0'
    compile group: 'org.springframework', name: 'spring-jms'
    compile group: 'org.apache.qpid', name: 'qpid-jms-client', version: '1.1.0'
    compile group: 'org.jadira.usertype', name: 'usertype.core', version: '7.0.0.CR1'

    compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.1.0'
    compile group: 'com.github.hmcts', name: 'java-logging', version: versions.reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '4.0.0'

    compile group: 'com.github.hmcts', name: 'sscs-common', version: '4.4.48'
    compile group: 'com.github.hmcts', name: 'sscs-pdf-email-common', version: '1.4.40'
    compile group: 'com.github.hmcts', name: 'sscs-job-scheduler', version: '1.5.7'

    compile group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '3.17.2-RELEASE'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.1'
    compile group: 'commons-fileupload', name: 'commons-fileupload', version: '1.4'
    compile group: 'io.github.openfeign.form', name: 'feign-form', version: '3.8.0'

    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.24'

    compile group: 'com.microsoft.azure', name: 'applicationinsights-logging-logback', version: '2.6.3'
    compile group: 'com.microsoft.azure', name: 'applicationinsights-spring-boot-starter', version: '2.6.3'

    compileOnly("org.springframework.boot:spring-boot-devtools")

    compile 'org.projectlombok:lombok:1.18.20'
    annotationProcessor "org.projectlombok:lombok:1.18.18"
    testCompileOnly 'org.projectlombok:lombok:1.18.18'
    testAnnotationProcessor "org.projectlombok:lombok:1.18.18"
    integrationTestCompileOnly 'org.projectlombok:lombok:1.18.18'
    integrationTestAnnotationProcessor "org.projectlombok:lombok:1.18.18"

    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.5.2') {
        exclude group: "com.vaadin.external.google", module:"android-json"
    }
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.11.2'
    testCompile 'pl.pragmatists:JUnitParams:1.1.1'

    runtime "org.springframework.boot:spring-boot-properties-migrator"
}

bootJar {
    archiveName 'track-your-appeal-notifications.jar'
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

run {
    def debug = System.getProperty("debug")
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5006']
    }
}

def dbHost = System.env.JOB_SCHEDULER_DB_HOST ?: "localhost"
def dbPort = System.env.JOB_SCHEDULER_DB_PORT ?: 5432
def dbName = System.env.JOB_SCHEDULER_DB_NAME ?: "sscsjobscheduler"
def dbUser = System.env.JOB_SCHEDULER_DB_USERNAME ?: "sscsjobscheduler"
def dbPass = System.env.JOB_SCHEDULER_DB_PASSWORD ?: "sscsjobscheduler"

flyway {
    url = 'jdbc:postgresql://' + dbHost + ':' + dbPort + '/' + dbName
    user = dbUser
    password = dbPass
}
